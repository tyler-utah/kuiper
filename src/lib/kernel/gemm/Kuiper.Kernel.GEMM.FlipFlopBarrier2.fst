module Kuiper.Kernel.GEMM.FlipFlopBarrier2

(* This module defines a barrier contract used by GEMMs that operate
   on Array2 (Tensor-backed) matrices. *)

#lang-pulse
open Kuiper
open Kuiper.Array.Vectorized
open Kuiper.EMatrix
open Kuiper.Math { even, odd }
open Kuiper.Tensor.Tiling

open Kuiper.Tensor
module SZ = Kuiper.SizeT
module CV = Kuiper.Kernel.GEMM.Copy.Vec2

(* [bp_sharing] is [m |-> Frac (1.0R /. nthr) em], which reaches [tensor_pts_to]
   only after unfolding the [pts_to] typeclass instances.  These reductions no
   longer happen inside the SMT-level slprop equality, so we do them by hand. *)
let unfold_bp_sharing () : FStar.Tactics.V2.Tac unit =
  FStar.Tactics.V2.norm [delta_attr [`%Pulse.Lib.Core.pulse_unfold];
                         delta_only [`%bp_sharing];
                         zeta; iota; primops];
  Pulse.Lib.Core.slprop_equiv_norm ()

(* ---- Strided chunk operations for Array2 ---- *)

ghost
fn split_array2_into_strided_chunks
  (#et : Type0) {| sized et, hvc : has_vec_cpy et |}
  (#rows #cols : nat)
  (#l : layout2 rows cols)
  (m : array2 et l)
  (#em : chest2 et rows cols)
  (nthr : pos)
  requires
    m |-> em
  ensures array_exists (core m)
  ensures
    pure (SZ.fits (l.ulen))
  ensures
    forall+ (tid : natlt nthr).
      own_strided_chunks m em nthr tid
{
  tensor_ilower2 m;
  forevery_flatten _;
  Classical.forall_intro (CV.in_chunk_covers_all (chunk et #_ #hvc) rows cols nthr);
  forevery_refine_ext #_ #(fun _ -> True)
    (fun (ij : (natlt rows & natlt cols)) ->
      exists tid. CV.in_chunk (chunk et #_ #hvc) rows cols nthr tid ij)
    _;
  Classical.forall_intro_3 (fun ij tid1 -> Classical.move_requires
                             (CV.in_chunk_no_overlap (chunk et #_ #hvc) rows cols nthr ij tid1));
  forevery_split_or_n _ _;
  ghost
  fn aux (tid : natlt nthr)
    requires
      forall+ (ij : (natlt rows & natlt cols){CV.in_chunk (chunk et #_ #hvc) rows cols nthr tid ij}).
        tensor_pts_to_cell m (idx2 ij._1 ij._2) (acc2 em ij._1 ij._2)
    ensures
      own_strided_chunks m em nthr tid
  {
    fold own_strided_chunks m em nthr tid;
  };
  forevery_map _ _ aux;
}

ghost
fn join_array2_from_strided_chunks
  (#et : Type0) {| sized et, hvc : has_vec_cpy et |}
  (#rows #cols : nat)
  (#l : layout2 rows cols)
  (m : array2 et l)
  (#em : chest2 et rows cols)
  (nthr : pos)
  requires pure (nonempty (abs (rows @| cols @| INil)))
  requires
    pure (SZ.fits (l.ulen))
  requires
    forall+ (tid : natlt nthr).
      own_strided_chunks m em nthr tid
  ensures
    m |-> em
{
  assert pure (SZ.fits (l.ulen));
  forevery_map
    (fun tid -> own_strided_chunks m em nthr tid)
    (fun tid -> forall+ (ij : (natlt rows & natlt cols){CV.in_chunk (chunk et #_ #hvc) rows cols nthr tid ij}).
        tensor_pts_to_cell m (idx2 ij._1 ij._2) (acc2 em ij._1 ij._2))
    fn tid { unfold own_strided_chunks m em nthr tid };
  forevery_join_or_n (fun (tid : natlt nthr) ij -> CV.in_chunk (chunk et #_ #hvc) rows cols nthr tid ij)
    (fun ij -> tensor_pts_to_cell m (idx2 ij._1 ij._2) (acc2 em ij._1 ij._2));
  Classical.forall_intro (CV.in_chunk_covers_all (chunk et #_ #hvc) rows cols nthr);
  Classical.forall_intro_3 (fun ij tid1 -> Classical.move_requires
                             (CV.in_chunk_no_overlap (chunk et #_ #hvc) rows cols nthr ij tid1));
  forevery_refine_ext #_
    #(fun (ij : (natlt rows & natlt cols)) ->
      exists tid. CV.in_chunk (chunk et #_ #hvc) rows cols nthr tid ij)
    (fun _ -> True)
    _;
  forevery_unflatten' _;
  tensor_iraise2 m;
}

ghost
fn join_array2_from_strided_chunks_underspec
  (#et : Type0) {| sized et, hvc : has_vec_cpy et |}
  (#rows #cols : nat)
  (#l : layout2 rows cols)
  (m : array2 et l)
  (nthr : pos)
  requires pure (nonempty (abs (rows @| cols @| INil)))
  requires
    pure (SZ.fits (l.ulen))
  requires
    forall+ (tid : natlt nthr).
      live_strided_chunks m nthr tid
  ensures
    live m
{
  forevery_map
    (fun (tid : natlt nthr) -> live_strided_chunks m nthr tid)
    (fun (tid : natlt nthr) -> exists* em. own_strided_chunks m em nthr tid)
    fn tid { unfold live_strided_chunks m nthr tid };

  let ff = forevery_exists #(natlt nthr) _;
  let em' : chest2 et rows cols =
    (mk2 fun i j ->
       let flat_idx : nat = i * cols + j in
       let chunk_idx = flat_idx / chunk et in
       let tid = chunk_idx % nthr in
       acc2 (ff tid) i j);

  forevery_map
    (fun (tid : natlt nthr) -> own_strided_chunks m (ff tid) nthr tid)
    (fun (tid : natlt nthr) -> own_strided_chunks m em' nthr tid)
    fn tid {
      unfold own_strided_chunks m (ff tid) nthr tid;
      forevery_map
        #(ij : (natlt rows & natlt cols){CV.in_chunk (chunk et #_ #hvc) rows cols nthr tid ij})
        (fun ij -> tensor_pts_to_cell m (idx2 ij._1 ij._2) (acc2 (ff tid) ij._1 ij._2))
        (fun ij -> tensor_pts_to_cell m (idx2 ij._1 ij._2) (acc2 em' ij._1 ij._2))
        fn ij { () };
      fold own_strided_chunks m em' nthr tid;
    };

  join_array2_from_strided_chunks m nthr;
  assert m |-> em';
}

(* ---- Barrier transform helpers ---- *)

ghost
fn bp_sharing_to_own_strided_chunks
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#rows #cols : pos)
  (l : full_layout2 rows cols)
  (sar : larray et (rows * cols))
  (em : chest2 et rows cols)
  (nthr : pos)
  (#_ : squash (chunk et /?+ cols))
  (#_ : squash (chunk et * nthr /?+ (rows * cols)))
  requires
    forall+ (_tid : natlt nthr).
      bp_sharing (from_array l sar) em nthr
  ensures
    forall+ (tid : natlt nthr).
      own_strided_chunks (from_array l sar) em nthr tid
{
  rewrite (forall+ (_tid : natlt nthr). bp_sharing (from_array l sar) em nthr)
       as (forall+ (_tid : natlt nthr). tensor_pts_to (from_array l sar) #(1.0R /. nthr) em)
       by unfold_bp_sharing ();
  tensor_gather_n (from_array l sar) nthr #1.0R;
  split_array2_into_strided_chunks (from_array l sar) nthr;
}

ghost
fn own_strided_chunks_to_bp_sharing
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#rows #cols : pos)
  (l : full_layout2 rows cols)
  (sar : larray et (rows * cols))
  (em : chest2 et rows cols)
  (nthr : pos)
  (#_ : squash (SZ.fits (l.ulen)))
  requires
    forall+ (tid : natlt nthr).
      own_strided_chunks (from_array l sar) em nthr tid
  ensures
    forall+ (_tid : natlt nthr).
      bp_sharing (from_array l sar) em nthr
{
  join_array2_from_strided_chunks (from_array l sar) nthr;
  tensor_share_n (from_array l sar) nthr #1.0R;
  rewrite (forall+ (_tid : natlt nthr). tensor_pts_to (from_array l sar) #(1.0R /. nthr) em)
       as (forall+ (_tid : natlt nthr). bp_sharing (from_array l sar) em nthr)
       by unfold_bp_sharing ();
}

ghost
fn bp_sharing_to_own_strided_chunks_underspec
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#rows #cols : pos)
  (l : full_layout2 rows cols)
  (sar : larray et (rows * cols))
  (nthr : pos)
  (#_ : squash (chunk et /?+ cols))
  (#_ : squash (chunk et * nthr /?+ (rows * cols)))
  requires
    forall+ (_tid : natlt nthr).
      exists* em.
        bp_sharing (from_array l sar) em nthr
  ensures
    forall+ (tid : natlt nthr).
      exists* em.
        own_strided_chunks (from_array l sar) em nthr tid
{
  rewrite (forall+ (_tid : natlt nthr). exists* (em : chest2 et rows cols).
             bp_sharing (from_array l sar) em nthr)
       as (forall+ (_tid : natlt nthr). exists* (em : chest2 et rows cols).
             tensor_pts_to (from_array l sar) #(1.0R /. nthr) em)
       by unfold_bp_sharing ();
  tensor_gather_n_underspec (from_array l sar) nthr #1.0R;
  with em. assert from_array l sar |-> em;
  split_array2_into_strided_chunks (from_array l sar) nthr;
  forevery_map
    (fun tid -> own_strided_chunks (from_array l sar) em nthr tid)
    (fun tid -> exists* em. own_strided_chunks (from_array l sar) em nthr tid)
    fn tid { };
}

ghost
fn own_strided_chunks_to_bp_sharing_underspec
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#rows #cols : pos)
  (l : full_layout2 rows cols)
  (sar : larray et (rows * cols))
  (nthr : pos)
  (#_ : squash (SZ.fits (l.ulen)))
  requires
    forall+ (tid : natlt nthr).
      exists* em.
        own_strided_chunks (from_array l sar) em nthr tid
  ensures
    forall+ (_tid : natlt nthr).
      exists* em.
        bp_sharing (from_array l sar) em nthr
{
  join_array2_from_strided_chunks_underspec (from_array l sar) nthr;
  with em. assert from_array l sar |-> em;
  tensor_share_n (from_array l sar) nthr #1.0R;
  rewrite (forall+ (_tid : natlt nthr). tensor_pts_to (from_array l sar) #(1.0R /. nthr) em)
       as (forall+ (_tid : natlt nthr). bp_sharing (from_array l sar) em nthr)
       by unfold_bp_sharing ();
  forevery_map
    (fun (tid : natlt nthr) -> bp_sharing (from_array l sar) em nthr)
    (fun (tid : natlt nthr) -> exists* em. bp_sharing (from_array l sar) em nthr)
    fn tid { };
}

(* ---- Even/odd barrier transforms ---- *)

ghost
fn even_barrier_p_to_q
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows})
  (#bk : pos{bk /?+ shared})
  (#bn : pos{bn /?+ cols})
  (l1 : full_layout2 bm bk)
  (l2 : full_layout2 bk bn)
  (sar1 : larray etA (bm * bk))
  (sar2 : larray etB (bk * bn))
  (nthr : pos)
  (#_ : squash (chunk etB /?+ bn))
  (#_ : squash (chunk etA /?+ bk))
  (#_ : squash (chunk etA * nthr /?+ (bm * bk)))
  (#_ : squash (chunk etB * nthr /?+ (bk * bn)))
  requires
    forall+ (tid : natlt nthr).
      (exists* em1. bp_sharing (from_array l1 sar1) em1 nthr) **
      (exists* em2. bp_sharing (from_array l2 sar2) em2 nthr)
  ensures
    forall+ (tid : natlt nthr).
      live_strided_chunks (from_array l1 sar1) nthr tid **
      live_strided_chunks (from_array l2 sar2) nthr tid
{
  forevery_unzip _ _;
  bp_sharing_to_own_strided_chunks_underspec l1 sar1 nthr;
  bp_sharing_to_own_strided_chunks_underspec l2 sar2 nthr;
  forevery_zip (fun (tid: natlt nthr) ->
      live_strided_chunks (from_array l1 sar1) nthr tid) _;
}

ghost
fn odd_barrier_p_to_q
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows})
  (#bk : pos{bk /?+ shared})
  (#bn : pos{bn /?+ cols})
  (l1 : full_layout2 bm bk)
  (l2 : full_layout2 bk bn)
  (sar1 : larray etA (bm * bk))
  (sar2 : larray etB (bk * bn))
  (nthr : pos)
  (bid : natlt (rows/bm * (cols/bn)))
  (it : natlt (2 * (shared / bk)))
  (#_ : squash (chunk etB /?+ bn))
  (#_ : squash (chunk etA /?+ bk))
  (#_ : squash (chunk etA * nthr /?+ (bm * bk)))
  (#_ : squash (chunk etB * nthr /?+ (bk * bn)))
  (#_ : squash (SZ.fits (l1.ulen)))
  (#_ : squash (SZ.fits (l2.ulen)))
  requires
    forall+ (tid : natlt nthr).
      own_strided_chunks (from_array l1 sar1) (ematrix_subtile eA bm bk (bid/(cols/bn)) (it/2)) nthr tid **
      own_strided_chunks (from_array l2 sar2) (ematrix_subtile eB bk bn (it/2) (bid%(cols/bn))) nthr tid
  ensures
    forall+ (tid : natlt nthr).
      bp_sharing (from_array l1 sar1) (ematrix_subtile eA bm bk (bid/(cols/bn)) (it/2)) nthr **
      bp_sharing (from_array l2 sar2) (ematrix_subtile eB bk bn (it/2) (bid%(cols/bn))) nthr
{
  forevery_unzip _ _;
  own_strided_chunks_to_bp_sharing l1 sar1 _ nthr;
  own_strided_chunks_to_bp_sharing l2 sar2 _ nthr;
  forevery_zip
    (fun (tid : natlt nthr) ->
      bp_sharing (from_array l1 sar1) (ematrix_subtile eA bm bk (bid/(cols/bn)) (it/2)) nthr)
      _;
}

(* ---- Main barrier_p_to_q_transform ---- *)

ghost
fn barrier_p_to_q_transform
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows})
  (#bk : pos{bk /?+ shared})
  (#bn : pos{bn /?+ cols})
  (l1 : full_layout2 bm bk)
  (l2 : full_layout2 bk bn)
  (sar1 : larray etA (bm * bk))
  (sar2 : larray etB (bk * bn))
  (nthr : pos)
  (bid : natlt (rows/bm * (cols/bn)))
  (#_ : squash (chunk etB /?+ bn))
  (#_ : squash (chunk etA /?+ bk))
  (#_ : squash (chunk etA * nthr /?+ (bm * bk)))
  (#_ : squash (chunk etB * nthr /?+ (bk * bn)))
  (#_ : squash (SZ.fits (l1.ulen)))
  (#_ : squash (SZ.fits (l2.ulen)))
  (it : nat)
  requires
    forall+ (tid : natlt nthr).
      barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
  ensures
    forall+ (tid : natlt nthr).
      barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
{
  if (it >= 2 * (shared / bk)) {
    forevery_map
      (fun (tid : natlt nthr) ->
        barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid)
      (fun (tid : natlt nthr) ->
        barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid)
      fn tid {
        rewrite barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid as emp;
        rewrite emp as barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid;
      };
  } else {
    let ev = even it;
    if ev {
      assert pure (it < 2 * (shared / bk));
      assert pure (even it);
      forevery_map
        (fun (tid : natlt nthr) ->
          barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid)
        (fun (tid : natlt nthr) ->
          (exists* em1. bp_sharing (from_array l1 sar1) em1 nthr) **
          (exists* em2. bp_sharing (from_array l2 sar2) em2 nthr)
        )
        fn tid {
          rewrite barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
              as (exists* em1. bp_sharing (from_array l1 sar1) em1 nthr) **
                  (exists* em2. bp_sharing (from_array l2 sar2) em2 nthr);
        };

      even_barrier_p_to_q eA eB l1 l2 sar1 sar2 nthr;

      forevery_map
        (fun (tid : natlt nthr) ->
          live_strided_chunks (from_array l1 sar1) nthr tid **
          live_strided_chunks (from_array l2 sar2) nthr tid)
        (fun (tid : natlt nthr) ->
          barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
        )
        fn tid {
          rewrite
            live_strided_chunks (from_array l1 sar1) nthr tid **
            live_strided_chunks (from_array l2 sar2) nthr tid
          as
            barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid;
        };
    } else {
      assert pure (it < 2 * (shared / bk));
      assert pure (odd it);
      forevery_map
        (fun (tid : natlt nthr) ->
          barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid)
        (fun (tid : natlt nthr) ->
          own_strided_chunks (from_array l1 sar1) (ematrix_subtile eA bm bk (bid/(cols/bn)) (it/2)) nthr tid **
          own_strided_chunks (from_array l2 sar2) (ematrix_subtile eB bk bn (it/2) (bid%(cols/bn))) nthr tid
        )
        fn tid {
          rewrite
            barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
          as
            own_strided_chunks (from_array l1 sar1) (ematrix_subtile eA bm bk (bid/(cols/bn)) (it/2)) nthr tid **
            own_strided_chunks (from_array l2 sar2) (ematrix_subtile eB bk bn (it/2) (bid%(cols/bn))) nthr tid;
        };

      odd_barrier_p_to_q eA eB l1 l2 sar1 sar2 nthr bid it;

      forevery_map
        (fun (tid : natlt nthr) ->
          bp_sharing (from_array l1 sar1) (ematrix_subtile eA bm bk (bid/(cols/bn)) (it/2)) nthr **
          bp_sharing (from_array l2 sar2) (ematrix_subtile eB bk bn (it/2) (bid%(cols/bn))) nthr)
        (fun (tid : natlt nthr) ->
          barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
        )
        fn tid {
          rewrite
            bp_sharing (from_array l1 sar1) (ematrix_subtile eA bm bk (bid/(cols/bn)) (it/2)) nthr **
            bp_sharing (from_array l2 sar2) (ematrix_subtile eB bk bn (it/2) (bid%(cols/bn))) nthr
          as
            barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid;
        };
    }
  }
}

(* ---- Per-thread fold/unfold helpers ---- *)

(* Arithmetic facts needed to reduce the guards of [barrier_p]/[barrier_q] on
   the concrete iterations [2*bkIdx] / [2*bkIdx+1].  They are stated with
   triggers because the guards are re-checked wherever those definitions are
   unfolded. *)
#push-options "--fuel 0 --ifuel 0 --z3rlimit 20"
let double_quot (shared bk : nat)
  : Lemma (requires bk > 0 /\ shared % bk = 0)
          (ensures 2 * shared / bk == 2 * (shared / bk))
          [SMTPat (2 * shared / bk)]
  = let q = shared / bk in
    FStar.Math.Lemmas.lemma_div_exact shared bk;
    assert (2 * shared == (2 * q) * bk);
    FStar.Math.Lemmas.multiple_division_lemma (2 * q) bk

let half_of_2x (x : nat) : Lemma ((2 * x) / 2 == x) [SMTPat ((2 * x) / 2)]
  = FStar.Math.Lemmas.cancel_mul_div x 2

let half_of_2x1 (x : nat) : Lemma ((2 * x + 1) / 2 == x) [SMTPat ((2 * x + 1) / 2)]
  = FStar.Math.Lemmas.division_addition_lemma 1 2 x
#pop-options

#push-options "--fuel 2"
ghost
fn fold_barrier_p_odd
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows})
  (#bk : pos{bk /?+ shared})
  (#bn : pos{bn /?+ cols})
  (#l1 : layout2 bm bk)
  (#l2 : layout2 bk bn)
  (m1 : array2 etA l1)
  (m2 : array2 etB l2)
  (nthr : pos)
  (bid : natlt (rows/bm * (cols/bn)))
  (mrow : nat{mrow == bid / (cols/bn)})
  (mcol : nat{mcol == bid % (cols/bn)})
  (bkIdx : natlt (shared / bk))
  (tid : natlt nthr)
  requires
    own_strided_chunks m1 (ematrix_subtile eA bm bk mrow bkIdx) nthr tid **
    own_strided_chunks m2 (ematrix_subtile eB bk bn bkIdx mcol) nthr tid
  ensures
    barrier_p eA eB m1 m2 nthr bid (2 * bkIdx + 1) tid
{
  rewrite
    own_strided_chunks m1 (ematrix_subtile eA bm bk mrow bkIdx) nthr tid **
    own_strided_chunks m2 (ematrix_subtile eB bk bn bkIdx mcol) nthr tid
  as
    barrier_p eA eB m1 m2 nthr bid (2 * bkIdx + 1) tid;
}
#pop-options

#push-options "--fuel 2"
ghost
fn unfold_barrier_q_odd
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows})
  (#bk : pos{bk /?+ shared})
  (#bn : pos{bn /?+ cols})
  (#l1 : layout2 bm bk)
  (#l2 : layout2 bk bn)
  (m1 : array2 etA l1)
  (m2 : array2 etB l2)
  (nthr : pos)
  (bid : natlt (rows/bm * (cols/bn)))
  (mrow : nat{mrow == bid / (cols/bn)})
  (mcol : nat{mcol == bid % (cols/bn)})
  (bkIdx : natlt (shared / bk))
  (tid : natlt nthr)
  requires
    barrier_q eA eB m1 m2 nthr bid (2 * bkIdx + 1) tid
  ensures
    bp_sharing m1 (ematrix_subtile eA bm bk mrow bkIdx) nthr **
    bp_sharing m2 (ematrix_subtile eB bk bn bkIdx mcol) nthr
{
  rewrite
    barrier_q eA eB m1 m2 nthr bid (2 * bkIdx + 1) tid
  as
    bp_sharing m1 (ematrix_subtile eA bm bk mrow bkIdx) nthr **
    bp_sharing m2 (ematrix_subtile eB bk bn bkIdx mcol) nthr;
}
#pop-options

#push-options "--fuel 2"
ghost
fn fold_barrier_p_even
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows})
  (#bk : pos{bk /?+ shared})
  (#bn : pos{bn /?+ cols})
  (#l1 : layout2 bm bk)
  (#l2 : layout2 bk bn)
  (m1 : array2 etA l1)
  (m2 : array2 etB l2)
  (nthr : pos)
  (bid : natlt (rows/bm * (cols/bn)))
  (bkIdx : natlt (shared / bk))
  (tid : natlt nthr)
  requires
    (exists* em1. bp_sharing m1 em1 nthr) **
    (exists* em2. bp_sharing m2 em2 nthr)
  ensures
    barrier_p eA eB m1 m2 nthr bid (2 * bkIdx) tid
{
  rewrite
    (exists* em1. bp_sharing m1 em1 nthr) **
    (exists* em2. bp_sharing m2 em2 nthr)
  as
    barrier_p eA eB m1 m2 nthr bid (2 * bkIdx) tid;
}
#pop-options

#push-options "--fuel 2"
ghost
fn unfold_barrier_q_even
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows})
  (#bk : pos{bk /?+ shared})
  (#bn : pos{bn /?+ cols})
  (#l1 : layout2 bm bk)
  (#l2 : layout2 bk bn)
  (m1 : array2 etA l1)
  (m2 : array2 etB l2)
  (nthr : pos)
  (bid : natlt (rows/bm * (cols/bn)))
  (bkIdx : natlt (shared / bk))
  (tid : natlt nthr)
  requires
    barrier_q eA eB m1 m2 nthr bid (2 * bkIdx) tid
  ensures
    live_strided_chunks m1 nthr tid **
    live_strided_chunks m2 nthr tid
{
  rewrite
    barrier_q eA eB m1 m2 nthr bid (2 * bkIdx) tid
  as
    live_strided_chunks m1 nthr tid **
    live_strided_chunks m2 nthr tid;
}
#pop-options

(* Bridge raised shared tensors to the raw-array barrier contract. Normalize
   the contract projection explicitly, since SMT does not reduce it reliably. *)
private let unfold_fb_contract () : FStar.Tactics.V2.Tac unit =
  FStar.Tactics.V2.norm [delta_only [`%contract]; iota; primops];
  Pulse.Lib.Core.slprop_equiv_norm ()

ghost
fn bp_to_rin
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows}) (#bk : pos{bk /?+ shared}) (#bn : pos{bn /?+ cols})
  (l1 : full_layout2 bm bk) (l2 : full_layout2 bk bn)
  (sar1 : larray etA (bm * bk)) (sar2 : larray etB (bk * bn))
  (sa1 : array2 etA l1) (sa2 : array2 etB l2)
  (nthr : pos) (bid : natlt (rows/bm * (cols/bn)))
  (it : nat) (tid : natlt nthr)
  requires
    barrier_p eA eB sa1 sa2 nthr bid it tid **
    pure (sa1 == from_array l1 sar1 /\ sa2 == from_array l2 sar2)
  ensures
    (contract eA eB l1 l2 sar1 sar2 nthr bid).rin it tid
{
  rewrite each sa1 as (from_array l1 sar1);
  rewrite each sa2 as (from_array l2 sar2);
  rewrite barrier_p eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
       as (contract eA eB l1 l2 sar1 sar2 nthr bid).rin it tid
       by unfold_fb_contract ();
}

ghost
fn rout_to_bq
  (#etA #etB : Type0)
  {| sized etA, has_vec_cpy etA, sized etB, has_vec_cpy etB |}
  (#rows #shared #cols : pos)
  (eA : chest2 etA rows shared)
  (eB : chest2 etB shared cols)
  (#bm : pos{bm /?+ rows}) (#bk : pos{bk /?+ shared}) (#bn : pos{bn /?+ cols})
  (l1 : full_layout2 bm bk) (l2 : full_layout2 bk bn)
  (sar1 : larray etA (bm * bk)) (sar2 : larray etB (bk * bn))
  (sa1 : array2 etA l1) (sa2 : array2 etB l2)
  (nthr : pos) (bid : natlt (rows/bm * (cols/bn)))
  (it : nat) (tid : natlt nthr)
  requires
    (contract eA eB l1 l2 sar1 sar2 nthr bid).rout it tid **
    pure (sa1 == from_array l1 sar1 /\ sa2 == from_array l2 sar2)
  ensures
    barrier_q eA eB sa1 sa2 nthr bid it tid
{
  rewrite (contract eA eB l1 l2 sar1 sar2 nthr bid).rout it tid
       as barrier_q eA eB (from_array l1 sar1) (from_array l2 sar2) nthr bid it tid
       by unfold_fb_contract ();
  rewrite each (from_array l1 sar1) as sa1;
  rewrite each (from_array l2 sar2) as sa2;
}

module Kuiper.Sparse.Array.PtsTo

#lang-pulse

open Kuiper
open FStar.Tactics.V2 { exact }
open Kuiper.Array.Vectorized
open Kuiper.Sparse.Common


let chunk_cell_offset (k nthr tid ch i : nat)
  : Lemma (k * (nthr * ch) + (tid * ch + i) ==
      (k * nthr + tid) * ch + i /\
      i + (k * nthr + tid) * ch == (k * nthr + tid) * ch + i)
  = ()


(* Live *)

let slice_live
  (#et : Type0)
  (#l : nat)
  (a : larray et l)
  (#[full_default ()] f : perm)
  (i j : nat)
  : slprop
  = exists* s. pts_to_slice a #f i j s

let array_live_cell
  (#et : Type0)
  (#l : nat)
  (a : larray et l)
  (#[full_default ()] f : perm)
  (i : natlt l)
  : slprop
  // esto es medio choto
  // = exists* v. Cell (a <: array et) (i <: nat) |-> Frac f (v <: et)
  = exists* v. pts_to_cell a #f i v


(* Vector *)

unfold
let pts_to_vec
  (#a:Type u#0) {| sized a, has_vec_cpy a |}
  (#sz:nat)
  ([@@@mkey] x:larray a sz)
  (#[full_default ()] f : perm)
  ([@@@mkey] i : nat)
  (v : seq a)
: slprop
=
  pts_to_slice x #f i (i + chunk a) v

unfold
let pts_to_vec'
  (#a:Type) {| sized a, has_vec_cpy a |}
  (#sz:nat)
  ([@@@mkey] x:larray a sz)
  (#[full_default ()] f : perm)
  ([@@@mkey] i : nat)
  (v : seq a)
  (k : natle (len v - chunk a))
: slprop
= pts_to_vec x #f i (Seq.slice v k (k + chunk a))

let live_vec
  (#a:Type) {| sized a, has_vec_cpy a |}
  (#l : nat)
  (x :larray a l)
  (#[full_default ()] f : perm)
  (i : nat)
: slprop
= exists* v. pts_to_vec x #f i v


(* Thread sharing *)

let thread_slice_pts_to
  (#et : Type0)
  (#n : nat)
  (a : larray et n)
  (i j : natle n { i <= j })
  (#m : nat)
  (s : lseq et m)
  (k : natle (m - (j - i)))
  (nthr : nat) (tid : natlt nthr)
: slprop
=
  forall+ (x : natlt ((j - i - tid) `divup` nthr)).
    pts_to_cell a (i + x * nthr + tid <: nat) (s @! k + x * nthr + tid)

let thread_slice_pts_to_value
  (#et : Type0)
  (#n : nat)
  (a : larray et n)
  (i j : natle n { i <= j })
  (v : et)
  (nthr : nat) (tid : natlt nthr)
: slprop
=
  forall+ (x : natlt ((j - i - tid) `divup` nthr)).
    pts_to_cell a (i + x * nthr + tid) v

let thread_slice_live
  (#et : Type0)
  (#n : nat)
  (a : larray et n)
  (i j : natle n {i <= j})
  (nthr : nat) (tid : natlt nthr)
: slprop
=
  forall+ (k : natlt ((j - i - tid) `divup` nthr)).
    array_live_cell a (i + k * nthr + tid)


(* Vector thread sharing *)

let thread_pts_to_chunks
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#n : nat)
  ([@@@mkey] x : larray et n)
  (#m : nat)
  (s : lseq et m)
  (i nthr : nat)
  (tid : natlt nthr)
: Pure slprop
  (requires (nthr * chunk et) /? n /\ i + n <= m)
  (ensures fun _ -> true)
=
  forall+ (k : natlt (n / (nthr * chunk et))).
    pts_to_vec' x ((k * nthr + tid) * chunk et)
      s (i + (k * nthr + tid) * chunk et)

let thread_live_chunks
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#n : nat)
  ([@@@mkey] x : larray et n)
  (nthr : nat)
  (tid : natlt nthr)
: Pure slprop
  (requires (nthr * chunk et) /? n)
  (ensures fun _ -> true)
=
  forall+ (k : natlt (n / (nthr * chunk et))).
    live_vec x ((k * nthr + tid) * chunk et)



(* Helpers *)

open Kuiper.Bijection

let share_thread_ff
  (n : nat) (nthr : pos) (k : natlt n)
: (tid : natlt nthr & natlt ((n + (nthr - 1) - tid) / nthr))
= (| k % nthr, k / nthr |)

let share_thread_gg
  (n : nat) (nthr : pos)
  (tid : natlt nthr) (k : natlt ((n - tid) `divup` nthr))
: (natlt n)
= (k * nthr + tid)

let share_thread_bij (n : nat) (nthr : pos)
: bijection
  (natlt n)
  (tid : natlt nthr & natlt ((n - tid) `divup` nthr))
=
{
  ff = (fun k -> share_thread_ff n nthr k);
  gg = (fun (| tid, k |) -> share_thread_gg n nthr tid k);

  ff_gg = ez;
  gg_ff = ez;
}

ghost
fn thread_slice_share
  (#et : Type0)
  (#n : nat)
  (x : larray et n)
  (i j : natle n { i <= j })
  (#m : nat)
  (nthr : pos)
  requires slice_live x i j
  ensures
    (forall+ (tid : natlt nthr). thread_slice_live x i j nthr tid) **
    array_exists x
{
  unfold slice_live;
  with s. assert pts_to_slice x i j s;
  pts_to_slice_ref x i j;

  slice_to_cells x i j;
  forevery_iso (share_thread_bij (j - i) nthr) _;
  forevery_ext
    (fun r ->
      pts_to_cell x
        (i + (share_thread_bij (j - i) nthr).gg r)
        (s @! (share_thread_bij (j - i) nthr).gg r)
    )
    (fun (| tid, k |) ->
      pts_to_cell x (i + k * nthr + tid ) (s @! k * nthr + tid)
    );
  forevery_unflatten_dep' _;

  forevery_map #(natlt nthr)
    (fun tid -> forall+ (k : natlt (((j -i) - tid) `divup` nthr)).
      pts_to_cell x (i + k * nthr + tid) (s @! k * nthr + tid)
    )
    (fun tid -> thread_slice_live x i j nthr tid)
    fn tid {
      forevery_map #(natlt (((j -i) - tid) `divup` nthr))
        (fun k -> pts_to_cell x (i + k * nthr + tid) (s @! k * nthr + tid))
        (fun k -> array_live_cell x (i + k * nthr + tid))
        fn k {
          fold array_live_cell x (i + k * nthr + tid);
        };
      fold thread_slice_live x i j nthr tid;
    };
}

ghost
fn thread_slice_gather
  (#et : Type0)
  (#n : nat)
  (x : larray et n)
  (i j : natle n { i <= j })
  (#m : nat)
  (s : lseq et m)
  (k : natle (m - (j - i)))
  (nthr : pos)
  requires array_exists x
  requires
    (forall+ (tid : natlt nthr). thread_slice_pts_to x i j s k nthr tid)
  ensures pts_to_slice x i j (Seq.slice s k (k + (j - i)))
{
  let s' = Seq.slice s k (k + (j - i));

  forevery_map #(natlt nthr)
    (fun tid -> thread_slice_pts_to x i j s k nthr tid)
    (fun tid -> forall+ (h : natlt (((j -i) - tid) `divup` nthr)).
      pts_to_cell x (i + h * nthr + tid) (s' @! h * nthr + tid)
    )
    fn tid {
      unfold thread_slice_pts_to x i j s k nthr tid;
      forevery_ext #(natlt (((j -i) - tid) `divup` nthr))
        (fun h -> pts_to_cell x (i + h * nthr + tid) (s @! k + h * nthr + tid))
        (fun h -> pts_to_cell x (i + h * nthr + tid) (s' @! h * nthr + tid));
    };

  forevery_flatten_dep _;

  forevery_ext #(tid : natlt nthr & natlt (((j - i) - tid) `divup` nthr))
    (fun r ->
      pts_to_cell x (i + r._2 * nthr + r._1) (s' @! r._2 * nthr + r._1)
    )
    (fun r ->
      pts_to_cell x
        (i + (share_thread_bij (j - i) nthr).gg r)
        (s' @! (share_thread_bij (j - i) nthr).gg r)
    );

  forevery_iso_back (share_thread_bij (j - i) nthr)
    (fun r -> pts_to_cell x (i + r) (s' @! r));

  cells_to_slice x i j;
}

ghost
fn thread_slice_gather_value
  (#et : Type0)
  (#n : nat)
  (x : larray et n)
  (i j : natle n { i <= j })
  (v : et)
  (nthr : pos)
  requires array_exists x
  requires
    (forall+ (tid : natlt nthr). thread_slice_pts_to_value x i j v nthr tid)
  ensures pts_to_slice x i j (Seq.create (j - i) v)
{
  let s = Seq.create (j - i) v;
  forevery_map #(natlt nthr)
    (fun tid -> thread_slice_pts_to_value x i j v nthr tid)
    (fun tid -> forall+ (k : natlt (((j -i) - tid) `divup` nthr)).
      pts_to_cell x (i + k * nthr + tid) (s @! k * nthr + tid)
    )
    fn tid {
      unfold thread_slice_pts_to_value x i j v nthr tid;
      forevery_ext #(natlt (((j -i) - tid) `divup` nthr))
        (fun h -> pts_to_cell x (i + h * nthr + tid) v)
        (fun h -> pts_to_cell x (i + h * nthr + tid) (s @! h * nthr + tid));
    };
  forevery_flatten_dep _;
  forevery_ext #(tid : natlt nthr & natlt (((j - i) - tid) `divup` nthr))
    (fun r ->
      pts_to_cell x (i + r._2 * nthr + r._1) (s @! r._2 * nthr + r._1)
    )
    (fun r ->
      pts_to_cell x
        (i + (share_thread_bij (j - i) nthr).gg r)
        (s @! (share_thread_bij (j - i) nthr).gg r)
    );
  forevery_iso_back (share_thread_bij (j - i) nthr)
    (fun r -> pts_to_cell x (i + r) (s @! r));

  cells_to_slice x i j;
}

ghost
fn thread_share_chunks
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#n : nat)
  (x : larray et n)
  (nthr : pos)
  (#_: squash ((nthr * chunk et) /? n))
  requires live x
  ensures
    (forall+ (tid : natlt nthr). thread_live_chunks x nthr tid) **
    array_exists x
{
  array_to_slice x;
  with s. assert pts_to_slice x 0 n s;
  pts_to_slice_ref x 0 n;

  let ch = chunk et;

  array_slice_1 x;

  forevery_factor n (n / (nthr * ch)) (nthr * ch) _;

  forevery_map #(natlt (n / (nthr * ch)))
    (fun k ->
      forall+ (h : natlt (nthr * ch)).
        pts_to_cell x (k * (nthr * ch) + h) (s @! k * (nthr * ch) + h)
    )
    (fun k ->
      forall+ (tid : natlt nthr). live_vec x ((k * nthr + tid) * chunk et)
    )
    fn k {
      forevery_factor (nthr * ch) nthr ch _;
      forevery_map #(natlt nthr)
        (fun tid ->
          forall+ (i : natlt ch).
            pts_to_cell x
              (k * (nthr * ch) + (tid * ch + i))
              (s @! k * (nthr * ch) + (tid * ch + i))
        )
        (fun tid ->
          live_vec x ((k * nthr + tid) * chunk et)
        )
        fn tid {
          let v : lseq et ch = Seq.slice s
            ((k * nthr + tid) * ch)
            ((k * nthr + tid) * ch + ch);

          forevery_map #(natlt ch)
            (fun i ->
              pts_to_cell x
                (k * (nthr * ch) + (tid * ch + i))
                (s @! k * (nthr * ch) + (tid * ch + i)))
            (fun i ->
              pts_to_cell x ((k * nthr + tid) * ch + i) (v @! i))
            fn i {
              chunk_cell_offset k nthr tid ch i;
              Seq.lemma_index_slice s
                ((k * nthr + tid) * ch)
                ((k * nthr + tid) * ch + ch)
                i;
              rewrite each
                (k * (nthr * ch) + (tid * ch + i))
              as ((k * nthr + tid) * ch + i);
              rewrite each
                (s @! ((k * nthr + tid) * ch + i))
              as (v @! i);
            };
          forevery_rw_size ch
            (((k * nthr + tid) * ch) + ch - ((k * nthr + tid) * ch));

          cells_to_nonempty_slice x
            ((k * nthr + tid) * ch)
            ((k * nthr + tid) * ch + ch);

          rewrite each ch as (chunk et);
          fold live_vec x ((k * nthr + tid) * chunk et);
        };
    };
  rewrite each ch as (chunk et);

  forevery_commute _;

  forevery_map #(natlt nthr)
    (fun tid ->
      forall+ (k: natlt (n / (nthr * (chunk et)))).
        live_vec x ((k * nthr + tid) * (chunk et))
    )
    (fun tid -> thread_live_chunks x nthr tid)
    fn tid { fold thread_live_chunks x nthr tid };
}

ghost
fn thread_gather_chunks
  (#et : Type0) {| sized et, has_vec_cpy et |}
  (#n : nat)
  (x : larray et n)
  (#m : nat)
  (s : lseq et m)
  (i : natle (m - n))
  (nthr : pos)
  (#_: squash ((nthr * chunk et) /? n))
  requires array_exists x
  requires
    (forall+ (tid : natlt nthr). thread_pts_to_chunks x s i nthr tid)
  ensures x |-> Seq.slice s i (i + n)
{
  let ch = v (chunk et);
  forevery_map
    (fun tid -> thread_pts_to_chunks x s i nthr tid)
    (fun tid ->
      forall+ (j : natlt ch) (k : natlt (n / (nthr * ch))).
        pts_to_cell x (k * (nthr * ch) + (tid * ch + j))
          (s @! i + (k * (nthr * ch) + (tid * ch + j)))
    )
    fn tid {
      unfold thread_pts_to_chunks x s i nthr tid;
      forevery_map #(natlt (n / (nthr * (chunk et))))
        (fun k ->
          pts_to_vec' x ((k * nthr + tid) * chunk et)
            s (i + (k * nthr + tid) * chunk et)
        )
        (fun k ->
          forall+ (j : natlt ch).
            pts_to_cell x (k * (nthr * ch) + (tid * ch + j))
              (s @! i + (k * (nthr * ch) + (tid * ch + j)))
        )
        fn k {
          rewrite each (v (chunk et)) as ch;

          slice_to_cells x
            ((k * nthr + tid) * ch)
            ((k * nthr + tid) * ch + ch);

          forevery_rw_size _ ch;
          forevery_map #(natlt ch)
            (fun j ->
              pts_to_cell x ((k * nthr + tid) * ch + j)
                (Seq.index
                  (Seq.slice s
                    (i + (k * nthr + tid) * ch)
                    (i + (k * nthr + tid) * ch + ch))
                  j)
            )
            (fun j ->
              pts_to_cell x (k * (nthr * ch) + (tid * ch + j))
                (s @! i + (k * (nthr * ch) + (tid * ch + j)))
            )
            fn j {
              chunk_cell_offset k nthr tid ch j;
              Seq.lemma_index_slice s
                (i + (k * nthr + tid) * ch)
                (i + (k * nthr + tid) * ch + ch)
                j;
              rewrite each
                ((k * nthr + tid) * ch + j)
              as (k * (nthr * ch) + (tid * ch + j));
              rewrite each
                (Seq.index
                  (Seq.slice s
                    (i + (k * nthr + tid) * ch)
                    (i + (k * nthr + tid) * ch + ch))
                  j)
              as (s @! i + (k * (nthr * ch) + (tid * ch + j)));
            };
        };
      rewrite each (v (chunk et)) as ch;
      forevery_commute _;
    };

  forevery_unfactor (nthr * ch) _ _
    (fun h ->
      forall+ (k : natlt (n / (nthr * ch))).
        pts_to_cell x
          (k * (nthr * ch) + h)
          (s @! i + (k * (nthr * ch) + h))
    );
  forevery_commute _;
  forevery_unfactor n (n / (nthr * ch)) (nthr * ch)
    (fun k -> pts_to_cell x k (s @! i + k));
  forevery_ext _
    (fun (k : natlt n) ->
      pts_to_cell x k (Seq.slice s i (i + n) @! k)
    );
  array_unslice_1_with_exists x;
}
